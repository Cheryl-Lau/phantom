!--------------------------------------------------------------------------!
! The Phantom Smoothed Particle Hydrodynamics code, by Daniel Price et al. !
! Copyright (c) 2007-2019 The Authors (see AUTHORS)                        !
! See LICENCE file for usage and distribution conditions                   !
! http://phantomsph.bitbucket.io/                                          !
!--------------------------------------------------------------------------!
!+
!  MODULE: inject
!
!  DESCRIPTION: Handles wind particle injection
!
!  REFERENCES: None
!
!  OWNER: Lionel Siess
!
!  $Id$
!
!  RUNTIME PARAMETERS:
!    iboundary_spheres  -- number of boundary spheres (integer)
!    iwind_resolution   -- if<>0 set number of particles on the sphere, reset particle mass
!    sonic_type         -- find transonic solution (1=yes,0=no)
!    star_Lum           -- central star luminosity (Lsun)
!    star_Teff          -- central star effective temperature (K)
!    wind_alpha         -- fraction of the gravitational acceleration imparted to the gas
!    wind_expT          -- temperature law exponent (if wind_type=2)
!    wind_inject_radius -- radius of injection of the wind (au)
!    wind_mass_rate     -- wind mass loss rate (Msun/yr)
!    pulsation_period  -- stellar pulsation period (days)
!    piston_velocity    -- velocity amplitude of the pulsation (km/s)
!    wind_shell_spacing -- desired ratio of sphere spacing to particle spacing
!    wind_temperature   -- wind temperature at the injection point (K)
!    wind_type          -- stellar wind (1=isoT,2=T(r),3=adia,4=3+cooling,+10=with dust)
!    wind_velocity      -- velocity at which wind is injected (km/s)
!
!  DEPENDENCIES: bowen_dust, dim, dust_free_wind, dusty_wind, eos,
!    icosahedron, infile_utils, injectutils, io, part, physcon, timestep,
!    units, wind_profile
!+
!--------------------------------------------------------------------------
module inject
 use physcon, only: solarl
 implicit none
 character(len=*), parameter, public :: inject_type = 'wind'

 public :: init_inject,inject_particles,write_options_inject,read_options_inject
!
!--runtime settings for this module
!
! Read from input file
 integer, public:: iboundary_spheres = 5
 integer, public:: iwind_resolution = 0
 real, public::    outer_boundary_au = 10.
 real, public::    wind_expT = 0.5
 real, public::    wind_shell_spacing = 1.
 real, public::    wind_alpha = 0.
#ifndef BOWEN
 real, public::    star_Teff = 2500.
 real, public::    star_Lum = 10000. * solarl
#endif
#ifdef BOWEN
 integer, public:: wind_type = 3
 integer, public:: sonic_type = 0
 real, public::    star_Teff = 3000.
 real, public::    star_Lum = 5315. * solarl
 real, public::    pulsation_period
 real, public::    pulsation_period_days = 350.
 real, public::    piston_velocity_km_s = 3.
 real, public::    wind_velocity_km_s = 0.
 real, public::    wind_mass_rate_Msun_yr = 1.04d-7
 real, public::    wind_injection_radius_au = 1.2568
 real, public::    wind_temperature = 3000.
#elif NUCLEATION
 integer, public:: wind_type = 3
 integer, public:: sonic_type = 1
 real, public::    wind_velocity_km_s = 0.
 real, public::    wind_mass_rate_Msun_yr = 1.d-5
 real, public::    wind_injection_radius_au = 2.37686663
 real, public::    wind_temperature = 2500.
#elif ISOTHERMAL
 integer, public:: wind_type = 1
 integer, public:: sonic_type = 1
 real, public::    wind_velocity_km_s = 25.
 real, public::    wind_mass_rate_Msun_yr = 1.d-8
 real, public::    wind_injection_radius_au = 0.46524726
 real, public::    wind_temperature
#else
 integer, public:: wind_type = 3
 integer, public:: sonic_type = 0
 real, public::    wind_velocity_km_s = 35.
 real, public::    wind_mass_rate_Msun_yr = 1.d-8
 real, public::    wind_injection_radius_au = 1.7
 real, public::    wind_temperature = 3000.
#endif

 real, public::    Rstar

 private

 real :: dtpulsation = 1.d99
 real :: u_to_temperature_ratio,wind_mass_rate,piston_velocity,wind_velocity,&
      mass_of_spheres,time_between_spheres,neighbour_distance,mass_of_particles,&
      dr3,Rstar_cgs,wind_injection_radius,rho_ini
 integer :: particles_per_sphere,nwall_particles,iresolution

 logical, parameter :: wind_verbose = .false.
 integer, parameter :: wind_emitting_sink = 1
 real :: geodesic_R(0:19,3,3), geodesic_v(0:11,3)
 character(len=*), parameter :: label = 'inject_wind'

contains

!-----------------------------------------------------------------------
!+
!  Initialize reusable variables
!+
!-----------------------------------------------------------------------
subroutine init_inject(ierr)
 use io,             only:fatal
 use timestep,       only:tmax,dtmax
 use wind_equations, only:init_wind_equations
#ifdef BOWEN
 use bowen_dust,   only:setup_bowen
#else
 use part,         only:xyzmh_ptmass
#ifdef NUCLEATION
 use dusty_wind,     only:setup_dustywind,get_initial_wind_speed
#else
 use dust_free_wind, only:setup_wind,get_initial_wind_speed
#endif
#endif
 use physcon,      only:mass_proton_cgs, kboltz, Rg, days, km, au, years, solarm, pi
 use icosahedron,  only:compute_matrices, compute_corners
 use eos,          only:gmw,gamma,polyk
 use units,        only:unit_velocity, umass, utime, udist, unit_energ
 use part,         only:massoftype,igas,iboundary,xyzmh_ptmass,imloss,ilum,iTeff
 use io,           only:iverbose
 use injectutils,  only:get_sphere_resolution,get_parts_per_sphere,get_neighb_distance
 integer, intent(out) :: ierr
 integer :: ires_min,nzones_per_sonic_point
 real :: mV_on_MdotR,initial_wind_velocity_cgs,sonic(8),rho_inj,dist_to_sonic_point
 real :: dr,dp,mass_of_particles1
 logical :: verbose = .false.

 !
 ! return without error
 !

 ierr = 0
 if (iverbose>0) verbose = .true.
 !
 ! convert input parameters to code units
 !
#ifdef BOWEN
 pulsation_period = pulsation_period_days * (days/utime)
 piston_velocity  = piston_velocity_km_s * (km / unit_velocity)
 dtpulsation      = pulsation_period/50.
#else
 piston_velocity  = 0.d0
#endif
 wind_velocity    = wind_velocity_km_s * (km / unit_velocity)
 !wind_velocity    = max(wind_velocity,piston_velocity)
 wind_mass_rate   = wind_mass_rate_Msun_yr * (solarm/umass) / (years/utime)
 wind_injection_radius  = wind_injection_radius_au * au / udist
 if (gamma > 1.0001) then
    u_to_temperature_ratio = Rg/(gmw*(gamma-1.)) / unit_velocity**2
 else
    u_to_temperature_ratio = Rg/(gmw*2./3.) / unit_velocity**2
 endif
 Rstar = Rstar_cgs/udist
#ifdef ISOTHERMAL
 wind_temperature = polyk* mass_proton_cgs/kboltz * unit_velocity**2*gmw
#endif
 print *,' initial sound_speed = ',sqrt(gamma*(gamma-1)*wind_temperature*u_to_temperature_ratio)*unit_velocity,' cm/s'
 print *,Rstar_cgs
#ifdef BOWEN
 call setup_bowen(u_to_temperature_ratio,star_Lum,wind_injection_radius,&
      star_Teff,piston_velocity,wind_velocity,pulsation_period,&
      wind_temperature,iboundary_spheres*particles_per_sphere)
#else
 call init_wind_equations (xyzmh_ptmass(4,wind_emitting_sink), star_Teff, Rstar, &
      wind_expT, u_to_temperature_ratio, wind_type)
#ifdef NUCLEATION
 call setup_dustywind(xyzmh_ptmass(4,wind_emitting_sink), star_Lum, star_Teff, Rstar_cgs,&
         wind_expT, wind_mass_rate, u_to_temperature_ratio, wind_alpha, wind_type)
#else
 call setup_wind(xyzmh_ptmass(4,wind_emitting_sink), Rstar_cgs, wind_mass_rate, &
         u_to_temperature_ratio, wind_alpha, wind_temperature, wind_type)
#endif
!if (wind_type == 2 .and. wind_temperature /= star_Teff) then
!    wind_injection_radius_au = Rstar_cgs/au*(star_Teff/wind_temperature)**(1./wind_expT)
!    wind_injection_radius  = wind_injection_radius_au * au / udist
!endif
!wind_injection_radius = max(wind_injection_radius_au * au, Rstar_cgs) / udist
!Rstar = min(wind_injection_radius_au*au,Rstar_cgs)

! integrate wind equation to get initial velocity required to set the resolution and save 1D profile
 call get_initial_wind_speed(wind_injection_radius*udist,wind_temperature,initial_wind_velocity_cgs,sonic,sonic_type)
 wind_velocity = initial_wind_velocity_cgs/unit_velocity
#endif

 if (iwind_resolution == 0) then
    !
    ! resolution is specified in terms of number of smoothing lengths
    ! per distance to sonic point
    !
    nzones_per_sonic_point = 8
    dist_to_sonic_point = sonic(1)/udist-wind_injection_radius
    dr = abs(dist_to_sonic_point)/nzones_per_sonic_point
    rho_inj = wind_mass_rate/(4.*pi*wind_injection_radius**2*(piston_velocity+wind_velocity))
    mass_of_particles = rho_inj*dr**3
    massoftype(igas) = mass_of_particles
    print*,' suggesting ',mass_of_particles, ' based on desired dr = ',dr,' dist-to-sonic=',dist_to_sonic_point
    !
    ! compute the dimensionless resolution factor m V / (Mdot R)
    ! where m = particle mass and V, Mdot and R are wind parameters
    !
!    mass_of_particles = massoftype(igas)
    mV_on_MdotR = mass_of_particles*(piston_velocity+wind_velocity)/(wind_mass_rate*wind_injection_radius)
    !
    ! solve for the integer resolution of the geodesic spheres
    ! gives number of particles on the sphere via N = 20*(2*q*(q - 1)) + 12
    !
    iresolution = get_sphere_resolution(wind_shell_spacing,mV_on_MdotR)
    particles_per_sphere = get_parts_per_sphere(iresolution)
    neighbour_distance   = get_neighb_distance(iresolution)
    print *,'iwind_resolution equivalence = ',iresolution
 else
    if (wind_velocity+piston_velocity == 0.) call fatal(label,'zero input wind velocity')
    iresolution = iwind_resolution
    particles_per_sphere = get_parts_per_sphere(iresolution)
    neighbour_distance   = get_neighb_distance(iresolution)
    mass_of_particles = wind_shell_spacing*neighbour_distance * wind_injection_radius * &
         wind_mass_rate / (particles_per_sphere * (wind_velocity+piston_velocity))
    massoftype(igas) = mass_of_particles
    print *,'iwind_resolution unchanged = ',iresolution
 endif

 mass_of_spheres = mass_of_particles * particles_per_sphere
 rho_ini = wind_mass_rate / (4.*pi*wind_injection_radius**2*(piston_velocity+wind_velocity))
 dr3 = 3.*mass_of_spheres/(4.*pi*rho_ini)
 nwall_particles = iboundary_spheres*particles_per_sphere
 time_between_spheres  = mass_of_spheres / wind_mass_rate
 massoftype(iboundary) = mass_of_particles
 if (time_between_spheres > tmax)  then
    print *,'time_between_spheres = ',time_between_spheres,' < tmax = ',tmax
    call fatal(label,'no shell ejection : tmax < time_between_spheres')
 endif
 call compute_matrices(geodesic_R)
 call compute_corners(geodesic_v)

 if (iverbose >= 1) then
    mass_of_particles1 = wind_shell_spacing * get_neighb_distance(4) * wind_injection_radius * wind_mass_rate &
         / (get_parts_per_sphere(4) * (piston_velocity+wind_velocity))
    print*,'required mass of particle  = ',mass_of_particles1,' to get 492 particles per sphere'
    dp = neighbour_distance*wind_injection_radius
    dr = (piston_velocity+wind_velocity)*mass_of_particles*particles_per_sphere/wind_mass_rate
    print*,'particle separation on spheres = ',dp
    print*,'got dr/dp = ',dr/dp,' compared to desired dr on dp = ',wind_shell_spacing
 endif


 !logging
 print*,'mass_of_particles          = ',mass_of_particles
 print*,'particles per sphere       = ',particles_per_sphere
 print*,'distance between spheres   = ',wind_shell_spacing*neighbour_distance
 if (sonic_type == 1) then
    print*,'distance to sonic point    = ',sonic(1)/udist-wind_injection_radius
    print*,'sonic radius               = ',sonic(1)/udist,sonic(1)
    print*,'number of shells to sonic  = ',(sonic(1)/udist-wind_injection_radius)/(wind_shell_spacing*neighbour_distance)
    print*,'time_to_sonic_point        = ',sonic(4)/utime
 endif
 print*,'time_between_spheres       = ',time_between_spheres
 print*,'wind_temperature           = ',wind_temperature
 print*,'wind_type                  = ',wind_type
 print*,'wind_injection_radius      = ',wind_injection_radius
 print*,'stellar_radius             = ',Rstar_cgs / udist
#ifdef BOWEN
 print*,'number of ejected shells per pulsation period (should at least be > 10) ',pulsation_period/time_between_spheres
 print*,'width of the boundary layer/ R* (should be < 1) = ',1.-(iboundary_spheres*dr3)**(1./3.)/wind_injection_radius
 print*,'radial pulsation amplitude/ R* = ',piston_velocity*pulsation_period/(2.*pi*wind_injection_radius)
 print*,'pulsation period in code units = ',pulsation_period
 !sanity checks
 ! 1 - ensure that a minimum number of shells are ejected during a pulsation period
 if (pulsation_period/time_between_spheres < 10. ) print *,'WARNING! only ',pulsation_period/time_between_spheres,&
      ' shells will be ejected during a pulsation period'
 ! 2 - make sure the size of the boundary layer is not too big (< 0.2 injection_radius)
 if (1.-(iboundary_spheres*dr3)**(1./3.)/wind_injection_radius > 0.2)  print*,'WARNING! the width of the boundary layer = ',&
      1.-(iboundary_spheres*dr3)**(1./3.)/wind_injection_radius,' Rinject'
#else
 !save a few models before the particles reach the sonic point
 if (dtmax > sonic(4)/utime) print *,'WARNING! dtmax > time to sonic point'
 !minimum resolution required so a few shells can be inserted between the injection radius and the sonic point
 ires_min = iboundary_spheres*wind_shell_spacing*0.5257/(sonic(1)/udist/wind_injection_radius-1.)+.5
 if (iwind_resolution < ires_min) print *,'WARNING! resolution too low to pass sonic point : iwind_resolution < ',ires_min
#endif

xyzmh_ptmass(imloss,wind_emitting_sink) = wind_mass_rate
xyzmh_ptmass(ilum,wind_emitting_sink)   = star_Lum/(unit_energ/utime)
xyzmh_ptmass(iTeff,wind_emitting_sink)  = star_Teff

end subroutine init_inject

!-----------------------------------------------------------------------
!+
!  Main routine handling wind injection.
!+
!-----------------------------------------------------------------------
subroutine inject_particles(time,dtlast,xyzh,vxyzu,xyzmh_ptmass,vxyz_ptmass,&
                            npart,npartoftype,dtinject)
 use physcon,      only:pi,au
 use io,           only:fatal
 use dim,          only:store_dust_temperature
#ifdef BOWEN
 use bowen_dust,   only:pulsating_wind_profile
#elif NUCLEATION
 use dusty_wind,   only:dusty_wind_profile
#else
 use dust_free_wind, only:dust_free_wind_profile
#endif
 use part,         only:igas,iTeff,iboundary,nptmass,delete_particles_outside_sphere,dust_temp
 use partinject,   only:add_or_update_particle
 use injectutils,  only:inject_geodesic_sphere
 use units,        only:udist
 real,    intent(in)    :: time, dtlast
 real,    intent(inout) :: xyzh(:,:), vxyzu(:,:), xyzmh_ptmass(:,:), vxyz_ptmass(:,:)
 integer, intent(inout) :: npart
 integer, intent(inout) :: npartoftype(:)
 real,    intent(out)   :: dtinject
 integer :: outer_sphere, inner_sphere, inner_boundary_sphere, first_particle, i, ipart, nshell_released, nboundaries
 real    :: local_time, GM, r, v, u, rho, e, mass_lost, x0(3), v0(3), r2, r2max !, cs2max, dr, dp
#ifdef NUCLEATION
 integer, parameter :: nreleased = 50
 real :: JKmuS(7)
#elif BOWEN
 integer, parameter :: nreleased = 1
 real :: surface_radius
#else
 integer, parameter :: nreleased = 50
#endif
 character(len=*), parameter :: label = 'inject_particles'
 logical, save :: released = .false.

 if (nptmass > 0 .and. wind_emitting_sink <= nptmass) then
    x0 = xyzmh_ptmass(1:3,wind_emitting_sink)
    GM = xyzmh_ptmass(4,wind_emitting_sink)
    v0 = vxyz_ptmass(1:3,wind_emitting_sink)
 else
    x0 = 0.
    v0 = 0.
    GM = 0.
 endif

 !
 ! delete particles that exit the outer boundary
 !
 call delete_particles_outside_sphere(x0,outer_boundary_au*au/udist,npart)

 if (npart > 0) then
    nshell_released = nreleased
    nboundaries = iboundary_spheres
    !release particles and declare inner boundary shells as gas particles so they can exert some pressure
    ipart = igas
    if (.not.released) then
       do i = npart-nshell_released*particles_per_sphere+1,npart
          call add_or_update_particle(igas,xyzh(1:3,i),vxyzu(1:3,i),xyzh(4,i),vxyzu(4,i),i,i,npartoftype,xyzh,vxyzu)
       enddo
       released = .true.
    endif
 else
    !initialise domain with boundary particles
    ipart = iboundary
    nshell_released = 0
    nboundaries = iboundary_spheres+nreleased
 endif
 outer_sphere = floor((time-dtlast)/time_between_spheres) + 1 + nshell_released
 inner_sphere = floor(time/time_between_spheres)+nshell_released
 inner_boundary_sphere = inner_sphere + nboundaries

 !only one sphere can be ejected at a time
 if (inner_sphere-outer_sphere > nboundaries) call fatal(label,'ejection of more than 1 sphere, timestep likely too large!')

 do i=inner_boundary_sphere,outer_sphere,-1
    local_time = time + (iboundary_spheres+nreleased-i) * time_between_spheres

    !compute the radius, velocity, temperature, chemistry of a sphere at the current local time
    v = wind_velocity
#ifdef BOWEN
    call pulsating_wind_profile(time,local_time, r, v, u, rho, e, GM, i, &
         inner_sphere,inner_boundary_sphere,dr3,rho_ini)
#elif NUCLEATION
    r = Rstar
    call dusty_wind_profile(time,local_time, r, v, u, rho, e, GM, wind_temperature, JKmuS)
#else
    r = wind_injection_radius
    call dust_free_wind_profile(local_time, r, v, u, rho, e, GM)
#endif

    if (i > inner_sphere) then
       ! boundary sphere
       first_particle = (nboundaries-i+inner_sphere)*particles_per_sphere+1
       ! print '(" ##### boundary sphere ",i4,i7,3(i4),i7,9(1x,es12.5))',i,first_particle,inner_sphere,nboundaries,&
       !      outer_sphere,npart,time,local_time,r/xyzmh_ptmass(5,1),v,u,rho
#ifdef NUCLEATION
       call inject_geodesic_sphere(i, first_particle, iresolution, r, v, u, rho,  geodesic_R, geodesic_V, &
            npart, npartoftype, xyzh, vxyzu, ipart, x0, v0, JKmuS)
#else
       call inject_geodesic_sphere(i, first_particle, iresolution, r, v, u, rho,  geodesic_R, geodesic_V, &
            npart, npartoftype, xyzh, vxyzu, ipart, x0, v0)
#endif
       if (store_dust_temperature) dust_temp(first_particle:first_particle+particles_per_sphere-1) = &
            xyzmh_ptmass(iTeff,wind_emitting_sink)
    else
       ! ejected particles
#ifdef NUCLEATION
       call inject_geodesic_sphere(i, npart+1, iresolution, r, v, u, rho, geodesic_R, geodesic_V,&
            npart, npartoftype, xyzh, vxyzu, igas, x0, v0, JKmuS)
#else
       call inject_geodesic_sphere(i, npart+1, iresolution, r, v, u, rho, geodesic_R, geodesic_V,&
            npart, npartoftype, xyzh, vxyzu, igas, x0, v0)
#endif
       !initialize dust temperature to star's effective temperature
       if (store_dust_temperature) dust_temp(npart+1:npart+particles_per_sphere) = xyzmh_ptmass(iTeff,wind_emitting_sink)
       ! update the sink particle mass
       if (nptmass > 0 .and. wind_emitting_sink <= nptmass) then
          xyzmh_ptmass(4,wind_emitting_sink) = xyzmh_ptmass(4,wind_emitting_sink) - mass_of_spheres
       endif
       print '(" ##### eject sphere ",4(i4),i7,9(1x,es12.5))',i,inner_sphere,nboundaries,&
            outer_sphere,npart,time,local_time,r/xyzmh_ptmass(5,1),v,u,rho
    endif
    !cs2max = max(cs2max,gamma*(gamma-1)*u)
 enddo
 ! update sink particle properties
 if (nptmass > 0 .and. wind_emitting_sink <= nptmass) then
    mass_lost = mass_of_spheres * (inner_sphere-outer_sphere+1)
    xyzmh_ptmass(4,wind_emitting_sink) = xyzmh_ptmass(4,wind_emitting_sink) - mass_lost
#ifdef BOWEN
    surface_radius = wind_injection_radius + piston_velocity*pulsation_period/(2.*pi)*sin(2.*pi*time/pulsation_period)
    !v2 xyzmh_ptmass(5,wind_emitting_sink) = (surface_radius**3-dr3)**(1./3.)
    xyzmh_ptmass(5,wind_emitting_sink) = sqrt(xyzh(1,1)**2+xyzh(2,1)**2+xyzh(3,1)**2)
#endif
 endif

 !
 ! return timestep constraint to ensure that time between sphere
 ! injections is adequately resolved
 !
 !dr = neighbour_distance*wind_injection_radius
 !dtinject = 0.25*dr/sqrt(cs2max)
 dtinject = min(0.2*time_between_spheres,dtpulsation)

end subroutine inject_particles

!-----------------------------------------------------------------------
!+
!  Writes input options to the input file
!+
!-----------------------------------------------------------------------
subroutine write_options_inject(iunit)
 use dim,          only: maxvxyzu
 use infile_utils, only: write_inopt
 integer, intent(in) :: iunit

 call write_inopt(wind_velocity_km_s,'wind_velocity','injection wind velocity (km/s, if sonic_type = 0)',iunit)
#if defined (BOWEN)
 call write_inopt(pulsation_period_days,'pulsation_period','stellar pulsation period (days)',iunit)
 call write_inopt(piston_velocity_km_s,'piston_velocity','velocity amplitude of the pulsation (km/s)',iunit)
#endif
 call write_inopt(wind_injection_radius_au,'wind_inject_radius','wind injection radius (au)',iunit)
 call write_inopt(wind_mass_rate_Msun_yr,'wind_mass_rate','wind mass loss rate (Msun/yr)',iunit)
 if (maxvxyzu==4) then
    call write_inopt(wind_temperature,'wind_temperature','wind temperature at the injection point (K)',iunit)
 endif
 call write_inopt(iwind_resolution,'iwind_resolution','if<>0 set number of particles on the sphere, reset particle mass',iunit)
 call write_inopt(wind_shell_spacing,'wind_shell_spacing','desired ratio of sphere spacing to particle spacing',iunit)
 call write_inopt(iboundary_spheres,'iboundary_spheres','number of boundary spheres (integer)',iunit)
#ifndef BOWEN
 call write_inopt(sonic_type,'sonic_type','find transonic solution (1=yes,0=no)',iunit)
#endif
#ifndef ISOTHERMAL
 call write_inopt(wind_type,'wind_type','stellar wind (1=isoT,2=T(r),3=adia,4=3+cooling)',iunit)

 call write_inopt(star_Lum/solarl,'star_Lum','central star luminosity (Lsun)',iunit)
 call write_inopt(star_Teff,'star_Teff','central star effective temperature (K)',iunit)
 call write_inopt(wind_expT,'wind_expT','temperature law exponent (if wind_type=2)', iunit)
#endif
 call write_inopt(outer_boundary_au,'outer_boundary','kill gas particles outside this radius (au)',iunit)
end subroutine write_options_inject

!-----------------------------------------------------------------------
!+
!  Reads input options from the input file.
!+
!-----------------------------------------------------------------------
subroutine read_options_inject(name,valstring,imatch,igotall,ierr)
 use options, only:iwind
 use io,      only:fatal
 use physcon, only:pi,steboltz,au
 character(len=*), intent(in)  :: name,valstring
 logical, intent(out) :: imatch,igotall
 integer,intent(out) :: ierr

 integer, save :: ngot = 0
 integer :: noptions
 logical :: isowind = .true.
 character(len=30), parameter :: label = 'read_options_inject'

 imatch  = .true.
 igotall = .false.
 select case(trim(name))
 case('outer_boundary')
    read(valstring,*,iostat=ierr) outer_boundary_au
 case('wind_velocity')
    read(valstring,*,iostat=ierr) wind_velocity_km_s
    ngot = ngot + 1
    if (wind_velocity_km_s < 0.)    call fatal(label,'invalid setting for wind_velocity (<0)')
 case('wind_inject_radius')
    read(valstring,*,iostat=ierr) wind_injection_radius_au
    ngot = ngot + 1
    if (wind_injection_radius_au < 0.) call fatal(label,'invalid setting for wind_inject_radius (<0)')
 case('wind_temperature')
    read(valstring,*,iostat=ierr) wind_temperature
    ngot = ngot + 1
    isowind = .false.
    if (wind_temperature < 0.)    call fatal(label,'invalid setting for wind_temperature (<0)')
 case('iwind_resolution')
    read(valstring,*,iostat=ierr) iwind_resolution
    ngot = ngot + 1
    if (iwind_resolution < 0) call fatal(label,'iwind_resolution must be bigger than zero')
 case('iboundary_spheres')
    read(valstring,*,iostat=ierr) iboundary_spheres
    ngot = ngot + 1
    if (iboundary_spheres <= 0) call fatal(label,'iboundary_spheres must be > 0')
 case('wind_shell_spacing')
    read(valstring,*,iostat=ierr) wind_shell_spacing
    ngot = ngot + 1
    if (wind_shell_spacing <= 0.) call fatal(label,'wind_shell_spacing must be >=0')
 case('sonic_type')
    read(valstring,*,iostat=ierr) sonic_type
    ngot = ngot + 1
    if (sonic_type < 0 .or. sonic_type > 1 ) call fatal(label,'invalid setting for sonic_type ([0,1])')
 case('wind_mass_rate')
    read(valstring,*,iostat=ierr) wind_mass_rate_Msun_yr
    ngot = ngot + 1
    if (wind_mass_rate_Msun_yr < 0.) call fatal(label,'invalid setting for wind_mass_rate (<0)')
#ifndef ISOTHERMAL
 case('star_Lum')
    read(valstring,*,iostat=ierr) star_Lum
    star_Lum = star_Lum * solarl
    ngot = ngot + 1
    if (star_Lum < 0.) call fatal(label,'invalid setting for star_Lum (<0)')
 case('star_Teff')
    read(valstring,*,iostat=ierr) star_Teff
    ngot = ngot + 1
    if (star_Teff < 0.)    call fatal(label,'invalid setting for star_Teff (<0)')
 case('wind_type')
    read(valstring,*,iostat=ierr) wind_type
    ngot = ngot + 1
    if (wind_type < 0 .or. wind_type > 4 ) call fatal(label,'invalid setting for wind_type ([0,3])')
    iwind = wind_type
 case('wind_expT')
    read(valstring,*,iostat=ierr) wind_expT
    ngot = ngot + 1
#endif
#ifdef BOWEN
 case('pulsation_period')
    read(valstring,*,iostat=ierr) pulsation_period_days
    ngot = ngot + 1
    if (pulsation_period_days < 0.) call fatal(label,'invalid setting for pulsation_period (<0)')
 case('piston_velocity')
    read(valstring,*,iostat=ierr) piston_velocity_km_s
    !wind_velocity_km_s = 0. ! set wind veolicty to zero when pulsating star
    ngot = ngot + 1
    if (piston_velocity_km_s <= 0. .and. wind_velocity_km_s <= 0.) call fatal(label,'invalid setting for wind_osc_vamp (<0)')
#endif
 case default
    imatch = .false.
 end select
#ifdef BOWEN
 noptions = 13
#else
 noptions = 12
#ifdef NUCLEATION
 noptions = 14
#elif ISOTHERMAL
 noptions = 7
#endif
#endif
 !debug
 !print '(a26,i3,i3)',trim(name),ngot,noptions
 igotall = (ngot >= noptions)
 if (igotall .and. trim(name) /= '') then
#ifdef ISOTHERMAL
    Rstar_cgs = wind_injection_radius_au *au
#else
    Rstar_cgs = sqrt(star_Lum/(4.*pi*steboltz*star_Teff**4))
    !if you launch the wind inside the photosphere, make sure the wind temperature >= star's effective temperature
    if (wind_injection_radius_au <= Rstar_cgs/au) then
       print *,'invalid setting for wind_inject_radius < Rstar (au)',wind_injection_radius_au,Rstar_cgs/au
       !call fatal(label,'invalid setting for wind_inject_radius (< Rstar)')
    endif
    ! if (wind_temperature < star_Teff) then
    !    print *,'Twind < star_Teff',wind_temperature,star_Teff
    !    !call fatal(label,'invalid setting for wind_temperature (Twind < star_Teff)')
    ! endif
#endif
 endif
 if (trim(name) == '') ngot = 0
end subroutine read_options_inject

end module inject
